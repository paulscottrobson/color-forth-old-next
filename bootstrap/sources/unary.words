// ***************************************************************************************
// ***************************************************************************************
//
//		Name : 		unary.words
//		Author :	Paul Robson (paul@robsons.org.uk)
//		Date : 		3rd December 2018
//		Purpose :	Source code for bootstrap ColorFORTH
//
// ***************************************************************************************
// ***************************************************************************************

@word[ret]		-

		ld 		a,d 								// 1's complement DE
		cpl
		ld 		d,a
		ld		a,e
		cpl
		ld 		e,a

// ***************************************************************************************

@copies[ret]	2*

		ex 		de,hl
		add 	hl,hl
		ex 		de,hl

// ***************************************************************************************

@copies[ret]	4*

		ex 		de,hl
		add 	hl,hl
		add 	hl,hl
		ex 		de,hl

// ***************************************************************************************

@copies[ret]	16*

		ex 		de,hl
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
		ex 		de,hl

// ***************************************************************************************

@copies[ret]	2/

		sra 	d
		rr 		e

// ***************************************************************************************

@word[ret]		abs

		bit 	7,d
		jr 		z,__absPositive
		ld 		a,d
		cpl
		ld 		d,a
		ld 		a,e
		cpl
		ld 		e,a
		inc 	de
__absPositive:

// ***************************************************************************************

@copies[ret]	bswap

		ld 		a,d
		ld 		d,e
		ld 		e,a

// ***************************************************************************************

@word[ret]		negate

		ld 		a,d 								// 2's complement DE
		cpl
		ld 		d,a
		ld		a,e
		cpl
		ld 		e,a
		inc 	de
		

