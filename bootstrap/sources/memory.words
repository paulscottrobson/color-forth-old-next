// ***************************************************************************************
// ***************************************************************************************
//
//		Name : 		memory.words
//		Author :	Paul Robson (paul@robsons.org.uk)
//		Date : 		3rd December 2018
//		Purpose :	Source code for bootstrap ColorFORTH 
//
// ***************************************************************************************
// ***************************************************************************************

@copies[hl] 	b!

		pop 	bc									// data in C
		ld 		a,c 								// write it.
		ld 		(de),a
		pop 	de 									// restore TOS

// ***************************************************************************************

@copies[ix] 	!
		pop 	hl 									// data in HL, address in DE
		ex 		de,hl 								// address in HL, data in DE
		ld 		(hl),e
		inc 	hl
		ld 		(hl),d
		pop 	de 									// restore TOS

// ***************************************************************************************


@word[ix] 		+!

		pop 	hl 									// data in HL, address in DE
		ld 		a,(de) 								// lsb
		add 	a,l
		ld 		(de),a
		inc 	de 									// msb
		ld 		a,(de)
		adc 	a,h
		ld 		(de),a

// ***************************************************************************************
		
@copies[ret] 	b@
	
		ld		a,(de) 								// read
		ld 		e,a 								// put in DE
		ld 		d,0

// ***************************************************************************************
		
@copies[ret] 	@
	
		ex 		de,hl
		ld 		e,(hl)
		inc 	hl
		ld 		d,(hl)

// ***************************************************************************************


@word[ix] 		or!

		pop 	hl 									// data in HL, address in DE
		ld 		a,(de) 								// lsb
		or 		l
		ld 		(de),a
		inc 	de 									// msb
		ld 		a,(de)
		or 		h
		ld 		(de),a

// ***************************************************************************************
		
@copies[ret] 	p@
	
		ld 		b,d
		ld 		c,e
		in 		e,(c)
		ld 		d,0

// ***************************************************************************************
		
@copies[ix] 	p!

		pop 	hl	
		ld 		b,d
		ld 		c,e
		out 	(c),l
		pop 	de
		

