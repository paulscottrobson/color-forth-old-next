; ***************************************************************************************
; ***************************************************************************************
;
;		Name : 		unary.words
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		16th November 2018
;		Purpose :	Unary operators
;
; ***************************************************************************************
; ***************************************************************************************

; @generator.ret bswap
		ld 		a,d
		ld 		d,e
		ld 		e,a
; @end

; *********************************************************************************

; @generator.ret 2*
		ex 		de,hl
		add 	hl,hl
		ex 		de,hl
; @end

; *********************************************************************************

; @generator.ret 2/
		srl 	d
		rr 		e
; @end

; *********************************************************************************

; @generator.ret 1+
		inc 	de
; @end

; *********************************************************************************

; @generator.ret 2+
		inc 	de
		inc 	de
; @end

; *********************************************************************************

; @generator.ret 1-
		dec 	de
; @end

; *********************************************************************************

; @generator.ret 2-
		dec 	de
		dec 	de
; @end

; *********************************************************************************

; @word.ret		-
		ld 		a,d
		cpl	
		ld 		d,a
		ld 		a,e
		cpl
		ld 		e,a
; @end

; *********************************************************************************

; @word.ret		negate
		ld 		a,d
		cpl	
		ld 		d,a
		ld 		a,e
		cpl
		ld 		e,a
		inc 	de
; @end

; *********************************************************************************

; @word.ret 	abs
		bit		7,d
		jr 		z,__IsPositive
		ld 		a,d
		cpl 
		ld 		d,a
		ld 		a,e
		cpl
		ld 		e,a
		inc 	de
__IsPositive:
; @end

; *********************************************************************************

; @word.ret 	0=
		ld 		a,d
		or 		e
		ld 		de,$0000
		jr 		nz,__IsNonZero
		dec 	de
__IsNonZero:
; @end

; *********************************************************************************

; @word.ret 	0<
		bit 	7,d
		ld 		de,$0000
		jr 		z,__IsPositive2
		dec 	de
__IsPositive2:
; @end
